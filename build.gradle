buildscript {
    ext.kotlin_version = '1.4.10'
    ext.ktor_version = '1.4.1'
    ext.exposed_version = '0.25.1'
    ext.testContainers_version = '1.15.0'
    ext.restAssured_version = '4.3.2'
    ext.wiremock_version = '2.25.1'
    ext.klogging_version = '2.0.3'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:1.14.2"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
    }
}

group 'org.adven.concordion.ext'
version = 'develop-SNAPSHOT'

subprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"
    apply plugin: 'kotlin'
    apply plugin: 'java'
    apply plugin: 'idea'

    repositories {
        mavenCentral()
        jcenter()
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileJava.options.encoding = 'utf-8'
    compileTestJava.options.encoding = 'utf-8'

    compileKotlin {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = '1.8'
        }
    }

    compileTestKotlin {
        kotlinOptions {
            freeCompilerArgs = ['-Xjsr305=strict']
            jvmTarget = '1.8'
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    }

    test {
        systemProperty "user.timezone", "Europe/Moscow"
        testLogging.showStandardStreams = true
        testLogging.exceptionFormat = 'full'
        testLogging.events('FAILED', 'PASSED')
        maxParallelForks = Runtime.runtime.availableProcessors()
    }

    apply from: "$rootDir/gradle/linters.gradle"
}

task publishExam {
    dependsOn ":exam-core:bintrayUpload"
    dependsOn ":exam-db:bintrayUpload"
    dependsOn ":exam-ws:bintrayUpload"
    dependsOn ":exam-mq:bintrayUpload"
    dependsOn ":exam-files:bintrayUpload"
    dependsOn ":exam-ui:bintrayUpload"
    dependsOn ":exam-ms:bintrayUpload"
}

task publishEnv {
    dependsOn ":env-core:bintrayUpload"
    dependsOn ":env-container:bintrayUpload"
    dependsOn ":env-db-db2:bintrayUpload"
    dependsOn ":env-db-mysql:bintrayUpload"
    dependsOn ":env-db-postgresql:bintrayUpload"
    dependsOn ":env-grpc-mock:bintrayUpload"
    dependsOn ":env-mq-rabbit:bintrayUpload"
    dependsOn ":env-mq-redis:bintrayUpload"
    dependsOn ":env-wiremock:bintrayUpload"
}

task publishAll {
    dependsOn(publishExam)
    dependsOn(publishEnv)
}